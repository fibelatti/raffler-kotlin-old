apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.applicationId
        versionCode rootProject.ext.versionMajor * 10000 + rootProject.ext.versionMinor * 100 + rootProject.ext.versionPatch
        versionName "$rootProject.ext.versionMajor.$rootProject.ext.versionMinor.$rootProject.ext.versionPatch"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        manifestPlaceholders = [fabricApiKey: FABRIC_API_KEY]
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", rootProject.ext.appName + " Dev"

            debuggable true
            minifyEnabled false
        }

        release {
            resValue "string", "app_name", rootProject.ext.appName

            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        test.java.srcDirs += 'src/test/resources'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$rootProject.ext.kotlin_version"

    // Material Design
    implementation "com.android.support:appcompat-v7:$rootProject.ext.appCompatVersion"
    implementation "com.android.support:design:$rootProject.ext.designVersion"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.ext.constraintLayoutVersion"
    implementation "com.android.support:cardview-v7:$rootProject.ext.cardViewVersion"

    // Dagger
    implementation "com.google.dagger:dagger:$rootProject.ext.daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$rootProject.ext.daggerVersion"

    // Rx
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.ext.rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.ext.rxAndroidVersion"

    // Persistence
    implementation "android.arch.persistence.room:runtime:$rootProject.ext.roomVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$rootProject.ext.roomVersion"

    // Fabric
    implementation("com.crashlytics.sdk.android:crashlytics:$rootProject.ext.crashlyticsVersion@aar") {
        transitive = true
    }
    implementation("com.crashlytics.sdk.android:answers:$rootProject.ext.answersVersion@aar") {
        transitive = true
    }

    // Testing
    testImplementation "junit:junit:$rootProject.ext.jUnitVersion"
    testImplementation "org.mockito:mockito-core:${rootProject.ext.mockitoVersion}"

    // 3rd Party Libraries
    // Charts
    implementation "me.itangqi.waveloadingview:library:$rootProject.ext.chartsVersion"
}
